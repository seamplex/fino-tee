# geometrical parameters

FUNCTION od(s) DATA {    # outside diameter in mm a function of the nominal size in inches
1 33.4
2 60.3
3 88.9
4 114.3
5 141.3
6 168.3
8 219.1
10 273.0
12 323.8
}

FUNCTION th(s) DATA {    # pipe thickness in mm for schedule 80
1  4.55
2  5.54
3  7.62
4  8.56
5  9.53
6  10.97
8  12.70
10 15.05
12 17.48
}

# main pipe is 12-inch schedule 80
b = 0.5*od(12)
a = b-th(12)

# swipe the tee diameter
PARAMETRIC td MIN 1 MAX 11 STEP 1

# branch's outer and internal diameter
d = 0.5*od(td)
c = d-th(td)

# prepare gmsh .geo file from m4 template
OUTPUT_FILE geo tee-%g.geo td DO_NOT_OPEN 
M4 INPUT_FILE_PATH tee.geo.m4  OUTPUT_FILE geo EXPAND a EXPAND b EXPAND c EXPAND d

# call gmsh
SHELL "gmsh -v 0 -3 tee-%g.geo" td

# read the generated mesh
INPUT_FILE msh tee-%g.msh td DO_NOT_OPEN 
MESH FILE msh DIMENSIONS 3

# uniform material properties
E = 200e3 # [MPa]
nu = 0.3

# internal pressure  
p = 10 # [MPa]

# boundary conditions
PHYSICAL_ENTITY axial    BC u=0
PHYSICAL_ENTITY right    BC u=0      0=v*z-w*y
PHYSICAL_ENTITY top      BC w=0      0=v*x-u*y
PHYSICAL_ENTITY half     BC v=0
PHYSICAL_ENTITY internal BC P=p

# solve!
FINO_STEP

# write vtk
OUTPUT_FILE out tee-%g.vtk td
MESH_POST FILE out VECTOR u v w sigma sigma1 sigma2 sigma3

# compute linearized stresses
FINO_LINEARIZE START_POINT d+b-a 0 +a END_POINT d+b-a 0 +b M M1   MB MB1
FINO_LINEARIZE START_POINT d+b-a +a 0 END_POINT d+b-a +b 0 M M2   MB MB2
FINO_LINEARIZE START_POINT d+b-a 0 -a END_POINT d+b-a 0 -b M M3   MB MB3


# analytical solution
# principal stresses along the radial coordinate (may be y or z)
s1(r) := p*a^2/(b^2-a^2) * (1 + b^2/r^2)
s2(r) := 2*nu*p*a^2/(b^2-a^2)
s3(r) := p*a^2/(b^2-a^2) * (1 - b^2/r^2)

# computation of main membrane stresses
M_1 = 1/(b-a)*integral(s1(r), r, a, b)
M_2 = 1/(b-a)*integral(s2(r), r, a, b)
M_3 = 1/(b-a)*integral(s3(r), r, a, b)
Ma = max(abs(M_1-M_2), abs(M_2-M_3), abs(M_3-M_1))

# computation of membrane plus bending stresses
MB_1 = M_1 + 6/(b-a)^2*integral(s1(r)*((a+b)/2-r), r, a, b)
MB_2 = M_2 + 6/(b-a)^2*integral(s2(r)*((a+b)/2-r), r, a, b)
MB_3 = M_3 + 6/(b-a)^2*integral(s3(r)*((a+b)/2-r), r, a, b)
MBa = max(abs(MB_1-MB_2), abs(MB_2-MB_3), abs(MB_3-MB_1))

# print parametric results
PRINT HEADER td Ma M1 M2 M3 MBa MB1 MB2 MB3 MBa-Ma MB1-M1 MB2-M2 MB3-M3
